#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЗагрузитьAPIКлюч();
	КонецЕсли;
	
	НастроитьЭлементыФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	СохранитьAPIКлюч(ТекущийОбъект.Ссылка);
	НастроитьЭлементыФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПроверитьПодключение(Команда)
	
	РезультатПроверки = ПроверитьПодключениеНаСервере();
	
	Если РезультатПроверки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатПроверки.ЭтоОшибка Тогда
		
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Ошибка подключения:%1%2'"),
			Символы.ПС,
			РезультатПроверки.ТекстОшибки);
		
		ПоказатьПредупреждение(, ТекстСообщения, , НСтр("ru = 'Проверка подключения'"));
		
	Иначе
		
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Подключение успешно!%1Ответ модели: %2%1Токенов использовано: %3'"),
			Символы.ПС,
			РезультатПроверки.ТекстОтвета,
			РезультатПроверки.ВсегоТокенов);
		
		ПоказатьПредупреждение(, ТекстСообщения, , НСтр("ru = 'Проверка подключения'"));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РаботаСAPIКлючом

&НаСервере
Процедура ЗагрузитьAPIКлюч()
	
	Попытка
		КлючAPI = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
			Объект.Ссылка,
			"APIКлюч");
			
		Если КлючAPI = Неопределено Тогда
			КлючAPI = "";
		КонецЕсли;
		
	Исключение
		КлючAPI = "";
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'КИИ.ЗагрузкаКлюча'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Справочники.КИИ_МоделиИИ,
			Объект.Ссылка,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьAPIКлюч(СсылкаНаМодель)
	
	Если ЗначениеЗаполнено(КлючAPI) Тогда
		
		Попытка
			ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
				СсылкаНаМодель,
				КлючAPI,
				"APIКлюч");
		Исключение
			ТекстОшибки = СтрШаблон(
				НСтр("ru = 'Ошибка сохранения API ключа: %1'"),
				ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'КИИ.СохранениеКлюча'", ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.Справочники.КИИ_МоделиИИ,
				СсылкаНаМодель,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
	Иначе
		
		Попытка
			ОбщегоНазначения.УдалитьДанныеИзБезопасногоХранилища(
				СсылкаНаМодель,
				"APIКлюч");
		Исключение
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'КИИ.УдалениеКлюча'", ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.Справочники.КИИ_МоделиИИ,
				СсылкаНаМодель,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти

#Область ОбщиеМетоды

&НаСервере
Процедура НастроитьЭлементыФормы()
	
	Элементы.ФормаПроверитьПодключение.Доступность = 
		ЗначениеЗаполнено(Объект.Ссылка) И ЗначениеЗаполнено(КлючAPI);
	
КонецПроцедуры

&НаСервере
Функция ПроверитьПодключениеНаСервере()
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Сначала запишите элемент справочника'"));
		Возврат Неопределено;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(КлючAPI) Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Сначала введите API ключ'"));
		Возврат Неопределено;
	КонецЕсли;
	
	Попытка
		
		ТестовыйПромпт = НСтр("ru = 'Ответь одним словом: Тест'");
		
		Результат = КИИ_КоннекторИИ.ЗапросКМодели(
			Объект.Ссылка,
			ТестовыйПромпт);
		
		Возврат Результат;
		
	Исключение
		
		РезультатОшибки = Новый Структура;
		РезультатОшибки.Вставить("ЭтоОшибка", Истина);
		РезультатОшибки.Вставить("ТекстОшибки", ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		Возврат РезультатОшибки;
		
	КонецПопытки;
	
КонецФункции

#КонецОбласти

#КонецОбласти

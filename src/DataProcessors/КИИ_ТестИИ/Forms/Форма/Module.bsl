#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗагрузитьНастройкиФормы();
	ОбновитьОтображениеОтвета();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьНастройкиФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЭкспертПриИзменении(Элемент)
	
	СохранитьНастройкиФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьВФонеПриИзменении(Элемент)
	
	СохранитьНастройкиФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура РазмерКонтекстаПриИзменении(Элемент)
	
	Если РазмерКонтекста < 0 Тогда
		РазмерКонтекста = 0;
	КонецЕсли;
	
	СохранитьНастройкиФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отправить(Команда)
	
	Если ПустаяСтрока(Промпт) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Введите промпт'"));
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Эксперт) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не заполнен Эксперт ИИ'"));
		Возврат;
	КонецЕсли;
	
	Элементы.Отправить.Доступность = Ложь;
	
	ТекущийПромпт = Промпт;
	Промпт = "";
	
	Если ВыполнятьВФоне Тогда
		ЗапуститьЗапросВФоне(ТекущийПромпт);
	Иначе
		ВыполнитьЗапросСинхронно(ТекущийПромпт);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)
	
	Промпт = "";
	ОчиститьИсториюНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ВыполнениеЗапроса

&НаКлиенте
Процедура ВыполнитьЗапросСинхронно(ТекущийПромпт)
	
	ДобавитьСообщениеВИсториюЛокально("user", ТекущийПромпт);
	
	РезультатЗапроса = ВыполнитьЗапросНаСервере(ТекущийПромпт);
	
	Элементы.Отправить.Доступность = Истина;
	
	ПроцессИтогиЗапроса(РезультатЗапроса);
	
КонецПроцедуры

&НаСервере
Функция ВыполнитьЗапросНаСервере(Знач ТекстПромпта)
	
	Модель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Эксперт, "Модель");
	СтруктураПромпта = СформироватьСтруктураПромпта(Эксперт, ТекстПромпта);
	
	Результат = КИИ_КоннекторИИ.ЗапросКМодели(Модель, СтруктураПромпта);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ЗапуститьЗапросВФоне(ТекстПромпта)
	
	ДобавитьСообщениеВИсториюЛокально("user", ТекстПромпта);
	
	Попытка
		ФоновоеЗадание = ЗапуститьЗапросВФонеНаСервере(ТекстПромпта);
	Исключение
		ТекстОшибки = НСтр("ru = 'Ошибка запуска фонового задания:'") + Символы.ПС 
			+ ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ДобавитьСообщениеВИсториюЛокально("error", ТекстОшибки);
		ПоказатьПредупреждение(, ТекстОшибки);
		
		Элементы.Отправить.Доступность = Истина;
		Возврат;
	КонецПопытки;
	
	Если ФоновоеЗадание = Неопределено Тогда
		Элементы.Отправить.Доступность = Истина;
		Возврат;
	КонецЕсли;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Запрос к модели ИИ...'");
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ВыводитьСообщения = Ложь;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьЗавершениеФоновогоЗадания", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ФоновоеЗадание, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция ЗапуститьЗапросВФонеНаСервере(Знач ТекстПромпта)
	
	Модель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Эксперт, "Модель");
	СтруктураПромпта = СформироватьСтруктураПромпта(Эксперт, ТекстПромпта);
	
	Возврат КИИ_КоннекторИИ.ЗапросКМоделиВФоне(Модель, СтруктураПромпта);
	
КонецФункции

&НаКлиенте
Процедура ОбработатьЗавершениеФоновогоЗадания(Результат, ДополнительныеПараметры) Экспорт
	
	Элементы.Отправить.Доступность = Истина;
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ТекстОшибки = НСтр("ru = 'Ошибка выполнения:'") + Символы.ПС;
		
		Если Результат.ИнформацияОбОшибке <> Неопределено Тогда
			ТекстОшибки = ТекстОшибки + ОбработкаОшибок.КраткоеПредставлениеОшибки(Результат.ИнформацияОбОшибке);
		Иначе
			ТекстОшибки = ТекстОшибки + Результат.КраткоеПредставлениеОшибки;
		КонецЕсли;
		
		ДобавитьСообщениеВИсториюЛокально("error", ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		РезультатЗапроса = ПолучитьРезультатИзАдреса(Результат.АдресРезультата);
		
		Если РезультатЗапроса <> Неопределено Тогда
			ПроцессИтогиЗапроса(РезультатЗапроса);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьРезультатИзАдреса(Знач АдресРезультата)
	
	Попытка
		Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
		Возврат Результат;
	Исключение
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'ИИКона.ПолучениеРезультатаИзХранилища'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Неопределено;
	КонецПопытки;
	
КонецФункции

&НаКлиенте
Процедура ПроцессИтогиЗапроса(РезультатЗапроса)
	
	Если РезультатЗапроса.ЭтоОшибка Тогда
		ДобавитьСообщениеВИсториюЛокально("error", РезультатЗапроса.ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	Метаинформация = СформироватьМетаинформацию(РезультатЗапроса);
	ДобавитьСообщениеВИсториюЛокально("assistant", РезультатЗапроса.ТекстОтвета, Метаинформация);
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСИсторией

&НаКлиенте
Процедура ДобавитьСообщениеВИсториюЛокально(Роль, Текст, Метаинформация = "")
	
	НоваяСтрока = ИсторияДиалога.Добавить();
	НоваяСтрока.Роль = Роль;
	НоваяСтрока.Текст = Текст;
	НоваяСтрока.Метаинформация = Метаинформация;
	
	ОбновитьОтображениеОтвета();
	
КонецПроцедуры

&НаКлиенте
Функция СформироватьМетаинформацию(РезультатЗапроса)
	
	МодельОтвета = ?(ПустаяСтрока(РезультатЗапроса.МодельОтвета), 
		"N/A", 
		РезультатЗапроса.МодельОтвета);
	
	Метаинформация = СтрШаблон(
		НСтр("ru = '⚡ Токены: %1 | ⏱ Время: %2с | ⚒ Модель: %3'"),
		РезультатЗапроса.ВсегоТокенов,
		Формат(РезультатЗапроса.ВремяОтветаСекунд, "ЧЦ=5; ЧДЦ=1"),
		МодельОтвета);
	
	Возврат Метаинформация;
	
КонецФункции

&НаСервере
Процедура ОчиститьИсториюНаСервере()
	
	ИсторияДиалога.Очистить();
	ОбновитьОтображениеОтвета();
	
КонецПроцедуры

#КонецОбласти

#Область ОтображениеДиалога

&НаСервере
Процедура ОбновитьОтображениеОтвета()
	
	Если ИсторияДиалога.Количество() = 0 Тогда
		ОтветHTML = СформироватьПустуюИсториюHTML();
		Возврат;
	КонецЕсли;
	
	Шаблон = Обработки.КИИ_ТестИИ.ПолучитьМакет("ШаблонДиалога").ПолучитьТекст();
	
	СообщенияHTML = "";
	Для Каждого Сообщение Из ИсторияДиалога Цикл
		СообщенияHTML = СообщенияHTML + СформироватьHTMLСообщения(Сообщение);
	КонецЦикла;
	
	HTML = СтрЗаменить(Шаблон, "[MESSAGES]", СообщенияHTML);
	
	ОтветHTML = HTML;
	
КонецПроцедуры

&НаСервере
Функция СформироватьПустуюИсториюHTML()
	
	HTML = "
	|<!DOCTYPE html>
	|<html>
	|<head>
	|    <meta charset='UTF-8'>
	|    <style>
	|        body {
	|            font-family: Arial;
	|            padding: 20px;
	|            text-align: center;
	|            color: #999;
	|        }
	|    </style>
	|</head>
	|<body>
	|    <p>✎ История диалога пуста</p>
	|    <p>Задайте вопрос, чтобы начать общение</p>
	|</body>
	|</html>";
	
	Возврат HTML;
	
КонецФункции

&НаСервере
Функция СформироватьHTMLСообщения(Сообщение)
	
	Класс = "";
	ИмяРоли = "";
	Эмодзи = ""; 
	
	Если Сообщение.Роль = "user" Тогда
		Класс = "user";
		ИмяРоли = НСтр("ru = 'Пользователь'");
		Эмодзи = "✎"; 
	ИначеЕсли Сообщение.Роль = "assistant" Тогда
		Класс = "assistant";
		ИмяРоли = НСтр("ru = 'ИИ эксперт'");
		Эмодзи = "⚒"; 
	Иначе 
		Класс = "error";
		ИмяРоли = НСтр("ru = 'Ошибка'");
		Эмодзи = "⚠"; 
	КонецЕсли;
	
	ТекстСообщения = ОбработатьТекстСообщения(Сообщение.Текст);
	
	HTML = СтрШаблон("
			|<div class='message %1'>
			|    <div class='role'>%2 %3</div>
			|    <div class='content'>%4</div>",
			Класс, Эмодзи, ИмяРоли, ТекстСообщения);
	
	Если Не ПустаяСтрока(Сообщение.Метаинформация) Тогда
		HTML = HTML + СтрШаблон("
		|    <div class='meta'>%1</div>", Сообщение.Метаинформация);
	КонецЕсли;
	
	HTML = HTML + "
			|</div>";
	
	Возврат HTML;
	
КонецФункции

&НаСервере
Функция ОбработатьТекстСообщения(Текст)
	
	ТипКонтента = ОпределитьТипКонтента(Текст);
	
	Если ТипКонтента = "HTML" Тогда
		Возврат СтрШаблон("<pre><code class='language-html'>%1</code></pre>", 
			КИИ_МаркдаунПарсерКлиентСервер.ЭкранироватьHTML(Текст));
		
	ИначеЕсли ТипКонтента = "JSON" Тогда
		Возврат СтрШаблон("<pre><code class='language-json'>%1</code></pre>", 
			КИИ_МаркдаунПарсерКлиентСервер.ЭкранироватьHTML(Текст));
		
	ИначеЕсли ТипКонтента = "Markdown" Тогда
		Возврат КИИ_МаркдаунПарсерКлиентСервер.ПреобразоватьВHTML(Текст);
	Иначе
		ТекстHTML = КИИ_МаркдаунПарсерКлиентСервер.ЭкранироватьHTML(Текст);
		ТекстHTML = СтрЗаменить(ТекстHTML, Символы.ПС, "<br>");
		Возврат ТекстHTML;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ОпределитьТипКонтента(Знач Текст)
	
	ТекстОбрезанный = СокрЛП(Текст);
	
	Если ЭтоПолныйHTMLДокумент(ТекстОбрезанный) Тогда
		Возврат "HTML";
	КонецЕсли;
	
	Если ЭтоJSON(ТекстОбрезанный) Тогда
		Возврат "JSON";
	КонецЕсли;
	
	Если ЭтоMarkdown(Текст) Тогда
		Возврат "Markdown";
	КонецЕсли;
	
	Если ЭтоHTMLФрагмент(ТекстОбрезанный) Тогда
		Возврат "HTML";
	КонецЕсли;
	
	Возврат "PlainText";
	
КонецФункции

&НаСервере
Функция ЭтоПолныйHTMLДокумент(Текст)
	
	ТекстНижний = НРег(Текст);
	
	Если СтрНачинаетсяС(ТекстНижний, "<!doctype html") Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если СтрНачинаетсяС(ТекстНижний, "<html") 
		И СтрНайти(ТекстНижний, "</html>") > 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Функция ЭтоJSON(Знач Текст)
	
	ТекстОбрезанный = СокрЛП(Текст);
	
	Если ПустаяСтрока(Текст) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПервыйСимвол = Лев(Текст, 1);
	ПоследнийСимвол = Прав(Текст, 1);
	
	ЭтоОбъект = (ПервыйСимвол = "{" И ПоследнийСимвол = "}");
	ЭтоМассив = (ПервыйСимвол = "[" И ПоследнийСимвол = "]");
	
	Если НЕ (ЭтоОбъект Или ЭтоМассив) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Попытка
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(Текст);
		ПрочитатьJSON(ЧтениеJSON);
		Возврат Истина;
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции

&НаСервере
Функция ЭтоHTMLФрагмент(Текст)
	
	ТекстНижний = НРег(Текст);
	
	НачинаетсяСТега = СтрНачинаетсяС(ТекстНижний, "<div")
		Или СтрНачинаетсяС(ТекстНижний, "<table")
		Или СтрНачинаетсяС(ТекстНижний, "<section")
		Или СтрНачинаетсяС(ТекстНижний, "<article");
	
	ЗаканчиваетсяТегом = СтрЗаканчиваетсяНа(ТекстНижний, "</div>")
		Или СтрЗаканчиваетсяНа(ТекстНижний, "</table>")
		Или СтрЗаканчиваетсяНа(ТекстНижний, "</section>")
		Или СтрЗаканчиваетсяНа(ТекстНижний, "</article>");
	
	Возврат НачинаетсяСТега И ЗаканчиваетсяТегом;
	
КонецФункции

&НаСервере
Функция ЭтоMarkdown(Знач Текст)
	
	Возврат КИИ_МаркдаунПарсерКлиентСервер.ЭтоMarkdown(Текст);
	
КонецФункции

#КонецОбласти

#Область НастройкиФормы

&НаСервере
Функция ПолучитьКлючНастроек()
	
	Возврат Метаданные.Обработки.КИИ_ТестИИ.ПолноеИмя();
	
КонецФункции

&НаСервере
Процедура ЗагрузитьНастройкиФормы()
	
	КлючОбъекта = ПолучитьКлючНастроек();
	
	Настройки = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		КлючОбъекта,
		"НастройкиФормы");
	
	Если ТипЗнч(Настройки) <> Тип("Структура") Тогда
		РазмерКонтекста = 5;
		Возврат;
	КонецЕсли;
	
	Если Настройки.Свойство("ПоследнийЭксперт") 
		И ЗначениеЗаполнено(Настройки.ПоследнийЭксперт) Тогда
		Эксперт = Настройки.ПоследнийЭксперт;
	КонецЕсли;
	
	Если Настройки.Свойство("ВыполнятьВФоне") Тогда
		ВыполнятьВФоне = Настройки.ВыполнятьВФоне;
	КонецЕсли;
	
	Если Настройки.Свойство("РазмерКонтекста") Тогда
		РазмерКонтекста = Настройки.РазмерКонтекста;
	Иначе
		РазмерКонтекста = 5;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиФормы()
	
	КлючОбъекта = ПолучитьКлючНастроек();
	
	Настройки = Новый Структура;
	Настройки.Вставить("ПоследнийЭксперт", Эксперт);
	Настройки.Вставить("ВыполнятьВФоне", ВыполнятьВФоне);
	Настройки.Вставить("РазмерКонтекста", РазмерКонтекста);
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		КлючОбъекта,
		"НастройкиФормы",
		Настройки);
	
КонецПроцедуры

#КонецОбласти

#Область Вспомогательные

&НаСервере
Функция СформироватьСтруктураПромпта(Знач Эксперт, Знач Промпт)
	
	Результат = Новый Структура;
	Результат.Вставить("СистемныйПромпт", "");
	Результат.Вставить("ПользовательскийПромпт", "");
	Результат.Вставить("ИсторияСообщений", Новый Массив);
	
	ПромптЭксперта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Эксперт, "ОписаниеЭксперта");
	Результат.СистемныйПромпт = ПромптЭксперта;
	Результат.ПользовательскийПромпт = Промпт;
	
	Если ИсторияДиалога.Количество() > 1 Тогда
		
		КоличествоДоступных = ИсторияДиалога.Количество() - 1;
		
		Если РазмерКонтекста = 0 Тогда
			НачальнаяПозиция = 0;
		Иначе
			НачальнаяПозиция = Макс(0, КоличествоДоступных - РазмерКонтекста);
		КонецЕсли;
		
		Для Индекс = НачальнаяПозиция По ИсторияДиалога.Количество() - 1 Цикл
			
			Сообщение = ИсторияДиалога[Индекс];
			
			Если Сообщение.Роль = "error" Тогда
				Продолжить;
			КонецЕсли;
			
			Если Индекс = ИсторияДиалога.Количество() - 1 И Сообщение.Роль = "user" Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаИстории = Новый Структура;
			СтрокаИстории.Вставить("Роль", Сообщение.Роль);
			СтрокаИстории.Вставить("Текст", Сообщение.Текст);
			
			Результат.ИсторияСообщений.Добавить(СтрокаИстории);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти
